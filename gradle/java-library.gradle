apply from: "$rootDir/gradle/snapshot.gradle"

if (isSnapshot) {
    version += "-SNAPSHOT"
}

/**
 * Generate source jar for project to expose javadoc and provide access to source code
 */
task sourceJar(type: Jar, dependsOn: compileKotlin) {
    group = "build"
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
    repositories {
        maven {
            credentials {
                username "${System.getenv("ARTIFACTORY_USER")}"
                password "${System.getenv("ARTIFACTORY_TOKEN")}"
            }
            url isSnapshot ? ossSnapshotLocal : ossReleaseLocal
        }
    }
}
