buildscript {
    repositories {
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url 'https://plugins.gradle.org/m2'
        }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:$artifactory_version"
        classpath 'org.openjfx:javafx-plugin:0.0.8-SNAPSHOT'
    }
}

apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'org.openjfx.javafxplugin'

repositories {
    mavenCentral()
    maven {
        url 'https://oss.jfrog.org/artifactory/libs-snapshot'
    }
}

dependencies {
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    api("org.springframework.boot:spring-boot-starter:$spring_version")
    api('io.github.dylmeadows:commonkt-javafx:0.0.1-SNAPSHOT') { changing = true }
    api('io.github.dylmeadows:reactor-javafx:0.0.1-SNAPSHOT') { changing = true }
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

javafx {
    version = '11.0.2'
    modules = [
        'javafx.base',
        'javafx.controls',
        'javafx.fxml'
    ]
    configuration = 'compileOnly'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version project.version
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
            maven = true
        }
        defaults {
            publications('mavenJava')
            publishConfigs('archives', 'published')
            publishBuildInfo = true
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
            maven = true
        }
    }
}